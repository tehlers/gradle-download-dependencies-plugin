buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'license'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile 'org.unbescape:unbescape:1.1.3.RELEASE'
}

group = 'net.idlestate'
archivesBaseName = 'gradle-download-dependencies-plugin'
// Please comply with the rules of Semantic Versioning (http://semver.org) and tag releases.
version = '1.4.4'

task sourceJar( type: Jar ) {
    from sourceSets.main.allGroovy
}

publishing {
    publications {
        gradlePlugin( MavenPublication ) {
            artifactId archivesBaseName
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description 'A Gradle plugin that downloads the dependencies of a Java project into a local repository that follows the maven structure'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    // Define in ~/.gradle/gradle.properties or as project property on startup
    user = bintray_user
    key = bintray_api_key

    publications = [ 'gradlePlugin' ]

    pkg {
        repo = 'maven'
        name = archivesBaseName
        licenses = [ 'Apache-2.0' ]
        vcsUrl = 'https://github.com/tehlers/gradle-download-dependencies-plugin.git'
        version {
            name = project.version
            desc = 'A Gradle plugin that downloads the dependencies of a Java project into a local repository that follows the maven structure'
        }
    }
}
